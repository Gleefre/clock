#+title: stopclock
#+author: Gleefre
#+email: varedif.a.s@gmail.com

#+language: en
#+options: toc:nil

=stopclock= is a library for measuring time using (stop)clocks.

#+toc: headlines 2

It allows you to create a clock, pause it and resume it.
You can also change its speed - for example you can have
a clock that runs backwards, or a clock that runs 2 times faster.

* Tutorial
** Load the stopclock library
   To use this library you need to clone this repository and load the ~clock~ system it defines.
   #+BEGIN_SRC lisp
   (ql:quickload :stopclock)  ; with quicklisp
   (asdf:load-system :stopclock)  ; with asdf
   (load "path/to/repository/stopclock.lisp")  ; without loading the system
   #+END_SRC
*** Package local nickname
    You can define package local nickname with most implementations (see [[https://lispcookbook.github.io/cl-cookbook/packages.html#package-local-nicknames-pln][Cookbook - packages]])
    You can do it in your package definition:
    #+BEGIN_SRC lisp
    (defpackage #:my-package
      (:use #:cl)
      (:local-nicknames (:sc :stopclock)))
    #+END_SRC
    Or during your REPL session:
    #+BEGIN_SRC lisp
    CL-USER> (add-package-local-nickname :sc :stopclock)
    #<PACKAGE "COMMON-LISP-USER">
    #+END_SRC
    The rest of this tutorial uses ~sc~ as a local nickname for ~stopclock~.
*** Tests
    You can run tests with asdf. Tests system depends on ~fiveam~ library.
    #+BEGIN_SRC lisp
    (asdf:test-system :stopclock/tests)  ; depends on fiveam
    ; ...
    ; Did 564 checks.
    ;    Pass: 564 (100%)
    ;    Skip: 0 ( 0%)
    ;    Fail: 0 ( 0%)
    T
    #+END_SRC
*** Don't :use package
    Note that you can't ~(:use-package :stopclock)~ because it defines ~stopclock:time~ function.
    It also defines ~stopclock:speed~ function that might cause package lock violation.

    You will need to shadow these functions:
    #+BEGIN_SRC lisp
    (defpackage #:my-package
      (:use #:cl #:stopclock)
      (:shadowing-import-from #:cl  ; or you can import #:stopclock version instead.
                              #:time #:speed))
    #+END_SRC
** Simple usage
   To create a clock use ~make-clock~ function.
   #+BEGIN_SRC lisp
   CL-USER> (defparameter *c* (sc:make-clock))
   *C*
   CL-USER> *c*
   #<CLOCK :TIME 1.98 SECONDS :RUNNING :SPEED x1>
   #+END_SRC
   You can start the clock with ~sc:run~ function and you can stop it with ~sc:stop~ function.
   #+BEGIN_SRC lisp
   CL-USER> (sc:stop *c*)
   #<CLOCK :TIME 11.08 SECONDS :PAUSED :SPEED x1>
   CL-USER> (sc:run *c*)
   #<CLOCK :TIME 11.08 SECONDS :RUNNING :SPEED x1>
   CL-USER> *c*
   #<CLOCK :TIME 15.70 SECONDS :RUNNING :SPEED x1>  ; the clock is running
   #+END_SRC
   To get current time on the clock you can use ~sc:time~ function.
   It returns the number of seconds passed.
   #+BEGIN_SRC lisp
   CL-USER> (float (sc:time *c*))
   32.039024
   #+END_SRC
   Finally, you can reset the current time on the clock with ~sc:reset~ function.
   #+BEGIN_SRC lisp
   CL-USER> (sc:reset *c*)
   #<CLOCK :TIME 0.00 SECONDS :RUNNING :SPEED x1>
   #+END_SRC
   It does not reset the paused / running state of the clock by default, but you can
   specify it with ~:paused T~ or ~:run T~ key arguments.
   #+BEGIN_SRC lisp
   CL-USER> (sc:reset *c* :paused T)
   #<CLOCK :TIME 0.00 SECONDS :PAUSED :SPEED x1>  ; clock is paused now
   CL-USER> (sc:reset *c*)
   #<CLOCK :TIME 0.00 SECONDS :PAUSED :SPEED x1>  ; clock is still paused
   CL-USER> (sc:reset *c* :run T)
   #<CLOCK :TIME 0.00 SECONDS :RUNNING :SPEED x1>  ; clock is running now
   #+END_SRC
** Copy clock
   Most defined functions, such as ~sc:stop~ and ~sc:run~, act destructively on passed clock.
   You can copy a clock with ~sc:copy-clock~.
   #+BEGIN_SRC lisp
   CL-USER> (list *c* *d*)
   (#<CLOCK :TIME 5.09 SECONDS :RUNNING :SPEED x1>
    #<CLOCK :TIME 5.09 SECONDS :RUNNING :SPEED x1>)
   CL-USER> (list *c* (sc:stop *d*))
   (#<CLOCK :TIME 11.72 SECONDS :RUNNING :SPEED x1>
    #<CLOCK :TIME 11.72 SECONDS :PAUSED :SPEED x1>)
   CL-USER> (list *c* *d*)
   (#<CLOCK :TIME 13.56 SECONDS :RUNNING :SPEED x1>
    #<CLOCK :TIME 11.72 SECONDS :PAUSED :SPEED x1>)
   #+END_SRC
** Clock parameters
   A clock has three parameters: ~time~, ~speed~ and whether it is paused or is running.
   ~speed~ refers to the speed with which the time on the clock changes.

   You can pass these parameters to the initialization function.
   For example you can create a paused clock that runs backwards with 5 seconds in the beginning:
   #+BEGIN_SRC lisp
   CL-USER> (sc:make-clock :paused t :time 5 :speed -1)
   #<CLOCK :TIME 5.00 SECONDS :PAUSED :SPEED -x1>
   CL-USER> (sc:run *)
   #<CLOCK :TIME 5.00 SECONDS :RUNNING :SPEED -x1>
   CL-USER> *
   #<CLOCK :TIME 3.03 SECONDS :RUNNING :SPEED -x1>
   #+END_SRC

   For each of these parameters a setfable accessor is defined:
   ~sc:time~, ~sc:paused~ and ~sc:speed~.
   #+BEGIN_SRC lisp
   CL-USER> (setf (sc:paused *c*) t)
   T
   CL-USER> (setf (sc:speed *c*) -10)
   -10
   CL-USER> (list (sc:time *c*)
                  (sc:paused *c*)
                  (sc:speed *c*))
   (322437913/1000000 T -10)
   CL-USER> (setf (sc:time *c*) 100.2)
   100.2
   CL-USER> *c*
   #<CLOCK :TIME 100.20 SECONDS :PAUSED :SPEED -x10>
   #+END_SRC
** State of the clock
   The paused / running state of the clock can be accessed with ~sc:paused~.
   The state can be set by combining ~sc:paused~ with ~setf~,
   It also can be set by functions ~sc:run~ (or a synonymous ~sc:start~),
   ~sc:pause~ (or a synonymous ~sc:stop~) and ~sc:toggle~.
   These function return the clock itself.
   #+BEGIN_SRC lisp
   CL-USER> (sc:stop *c*)
   #<CLOCK :TIME 100.20 SECONDS :RUNNING :SPEED -x10>
   CL-USER> (sc:run *c*)
   #<CLOCK :TIME -278.34 SECONDS :PAUSED :SPEED -x10>
   CL-USER> (sc:stop *c*)
   #<CLOCK :TIME -584.55 SECONDS :PAUSED :SPEED -x10>
   CL-USER> (sc:pause *c*)
   #<CLOCK :TIME -584.55 SECONDS :PAUSED :SPEED -x10>
   CL-USER> (sc:toggle *c*)
   #<CLOCK :TIME -584.56 SECONDS :RUNNING :SPEED -x10>
   CL-USER> (sc:toggle *c*)
   #<CLOCK :TIME -751.64 SECONDS :PAUSED :SPEED -x10>
   #+END_SRC
** Time on the clock
   The time on the clock can accessed with ~sc:time~.
   You can set the time by combining ~sc:time~ with ~setf~.
   There is also a ~sc:adjust~ function that increments / decrements the current time by a given delta.
   Unlike ~setf~ or ~incf~ it returns the clock itself.
   #+BEGIN_SRC lisp
   CL-USER> (setf (sc:time *c*) 0)
   0
   CL-USER> *c*
   #<CLOCK :TIME 0.00 SECONDS :PAUSED :SPEED -x10>
   CL-USER> (incf (sc:time *c*) 10)
   10
   CL-USER> *c*
   #<CLOCK :TIME 10.00 SECONDS :PAUSED :SPEED -x10>
   CL-USER> (sc:adjust *c* 20)
   #<CLOCK :TIME 30.00 SECONDS :PAUSED :SPEED -x10>
   #+END_SRC
** Speed of the clock
   The speed of the clock can accessed with ~sc:speed~.
   You can set it by combining ~sc:speed~ with ~setf~.
   There is also a ~sc:accelerate~ function that will multiply the speed by a given factor.
   Unlike ~setf~ or ~incf~ it returns the clock itself.
   #+BEGIN_SRC lisp
   CL-USER> (sc:run *c*)
   #<CLOCK :TIME 30.00 SECONDS :RUNNING :SPEED -x10>
   CL-USER> (setf (sc:speed *c*) 30)
   30
   CL-USER> *c*
   #<CLOCK :TIME 37.87 SECONDS :RUNNING :SPEED x30>
   CL-USER> (setf (sc:speed *c*) -100)
   -100
   CL-USER> *c*
   #<CLOCK :TIME 259.69 SECONDS :RUNNING :SPEED -x100>
   CL-USER> (sc:accelerate *c* -1)
   #<CLOCK :TIME -528.73 SECONDS :RUNNING :SPEED x100>
   CL-USER> (sc:accelerate *c* 1/100)
   #<CLOCK :TIME 312.19 SECONDS :RUNNING :SPEED x1>
   #+END_SRC
*** zero-clock-speed-error
    The speed of the clock cannot be equal to zero.
    If you try to set it to zero the ~sc:zero-clock-speed-error~ will be signalled.
** Reset the clock
   To reset the clock you can use ~sc:reset~ function.
   By default it only resets the time to 0.
   You can pass one of ~:paused~ or ~:run~ key arguments to
   set the clock's state to the corresponding value.
   You can also specify ~:speed~ and ~:time~ to be set.
   The function returns the clock itself.
   #+BEGIN_SRC lisp
   CL-USER> (sc:reset *c* :paused t)
   #<CLOCK :TIME 0.00 SECONDS :PAUSED :SPEED x1>
   CL-USER> (sc:reset *c* :run t)
   #<CLOCK :TIME 0.00 SECONDS :RUNNING :SPEED x1>
   CL-USER> (sc:reset *c* :speed 10)
   #<CLOCK :TIME 0.00 SECONDS :RUNNING :SPEED x10>
   CL-USER> (sc:reset *c* :time -10)
   #<CLOCK :TIME -10.00 SECONDS :RUNNING :SPEED x10>
   #+END_SRC
   The ~:paused~ arguments takes precedence over ~:run~:
   #+BEGIN_SRC lisp
   CL-USER> (sc:reset *c* :paused t :run t)
   #<CLOCK :TIME 0.00 SECONDS :PAUSED :SPEED x10>
   #+END_SRC
** Time source
   By default the clock will get current time with ~get-internal-real-time~ function.
   This behaviour can be changed by passing ~:time-source~ parameter to the constructor.
   This must be a function that returns the number of seconds when you call it.
   For convenience =clock= defines two functions: ~sc:real-time~ that uses ~get-internal-real-time~
   (but divides it by ~internal-time-units-per-second~) and ~sc:run-time~ that uses ~get-internal-run-time~
   instead.
   #+BEGIN_SRC lisp
   CL-USER> (let ((real-clock (sc:make-clock :paused nil))
                  (run-clock  (sc:make-clock :paused nil :time-source #'sc:run-time)))
              (sleep 5)
              (list real-clock run-clock))
   (#<CLOCK :TIME 5.00 SECONDS :RUNNING :SPEED x1>
    #<CLOCK :TIME 0.00 SECONDS :RUNNING :SPEED x1>)
   #+END_SRC
** Synchronized clocks
   It is impossible to start two clocks at the same time, since they may have different time sources.
   However, synchronized clocks can be obtained by using a third clock as the time source.
   Consider this example:
   #+BEGIN_SRC lisp
   CL-USER> (let ((1x (sc:make-clock))
                  (latency (sleep 0.01))
                  (5x (sc:make-clock :speed 5)))
              (declare (ignore latency))
              (sleep 1)
              (= (* 5 (sc:time 1x))
                 (sc:time 5x)))
   NIL
   #+END_SRC
   We create two clocks, one running 5 times faster than another.
   We also introduce an artificial latency between their creation.
   As a result they are out of sync.
   If we use the third clock as the time source paused during the creation of clocks, then
   the clocks are synchronized:
   #+BEGIN_SRC lisp
   CL-USER> (let* ((clock (sc:make-clock :paused t))
                   (1x (sc:make-clock :time-source (lambda () (sc:time clock))))
                   (latency (sleep 0.01))
                   (5x (sc:make-clock :time-source (lambda () (sc:time clock))
                                      :speed 5)))
              (declare (ignore latency))
              (sc:run clock)
              (sleep 1)
              (sc:stop clock)
              (= (* 5 (sc:time 1x))
                 (sc:time 5x)))
   T
   #+END_SRC

   For simplicity you can directly pass another clock as time source.
   Here is another example:
   #+BEGIN_SRC lisp
   CL-USER> (let* ((source-clock (sc:make-clock :paused t))
                   (up (sc:make-clock :time-source source-clock))
                   (down (sc:make-clock :time-source source-clock
                                        :speed -1 :time 50)))
              (sc:run source-clock)
              (format t "  up: ~a~%down: ~a~%" up down)
              (sleep 1)
              (format t "  up: ~a~%down: ~a~%" up down)
              (sc:stop source-clock)
              (= 50 (+ (sc:time up) (sc:time down))))
     up: #<CLOCK :TIME 0.00 SECONDS :RUNNING :SPEED x1>
   down: #<CLOCK :TIME 50.00 SECONDS :RUNNING :SPEED -x1>
     up: #<CLOCK :TIME 1.00 SECONDS :RUNNING :SPEED x1>
   down: #<CLOCK :TIME 49.00 SECONDS :RUNNING :SPEED -x1>
   T
   #+END_SRC
   Time on the clocks ~up~ and ~down~ will always add up to 50.
** Clock freeze
   If you want to read the time on synchronized clocks you need to pause the common source clock first.
   That means that the time spent on processing time values will not be tracked.
   Clock freeze solves this problem.
   When you ~sc:freeze~ the clock it freezes the time on the clock, which is identical to pausing it.
   However, when you ~sc:unfreeze~ it, the clock behaves as if it had not been frozen.
   #+BEGIN_SRC lisp
   CL-USER> (defparameter *c* (sc:make-clock))
   *C*
   CL-USER> (sc:freeze *c*)
   #<CLOCK :TIME 4.19 SECONDS :FREEZED :SPEED x1>
   CL-USER> *c*
   #<CLOCK :TIME 4.19 SECONDS :FREEZED :SPEED x1>
   CL-USER> (sc:unfreeze *c*)
   #<CLOCK :TIME 10.36 SECONDS :RUNNING :SPEED x1>  ; about 6 seconds elapsed during the freeze.
   #+END_SRC
   It also means that the paused clock will remain paused.
   #+BEGIN_SRC lisp
   CL-USER> (defparameter *c* (sc:make-clock :time 3 :paused t))
   *C*
   CL-USER> (sc:freeze *c*)
   #<CLOCK :TIME 3.00 SECONDS :PAUSED :SPEED x1>
   CL-USER> (sc:unfreeze *c*)
   #<CLOCK :TIME 3.00 SECONDS :PAUSED :SPEED x1>
   CL-USER> *
   #<CLOCK :TIME 3.00 SECONDS :PAUSED :SPEED x1>
   #+END_SRC
   =clock= also provides a macro ~sc:with-freeze~. Consider the previous example:
   #+BEGIN_SRC lisp
   CL-USER> (let* ((source-clock (sc:make-clock :paused t))
                   (up (sc:make-clock :time-source source-clock))
                   (down (sc:make-clock :time-source source-clock
                                        :speed -1 :time 50)))
              (sc:run source-clock)
              (loop repeat 5
                    do (sleep 0.1)
                    always (= 50 (sc:with-freeze source-clock
                                   (+ (sc:time up) (sc:time down))))))
   T
   #+END_SRC
   To keep the time read from ~up~ and ~down~ clocks in sync,
   we freeze their common source each time we need to read them.
* Bugs & Contributions
  Feel free to report bugs or make suggestions by filing an issue on github.

  Feel free to submit pull requests on github as well.
* License
  Copyright 2023 Gleefre

  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
